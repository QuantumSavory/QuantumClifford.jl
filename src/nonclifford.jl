#=
1. adding tests for basic correctness
2. single qubit gates / channels (and tests)
3. embedding single qubit gates - Stefan
4. pretty printing - Stefan
5. good docstrings
6. some superficial documentation
7. picking names
8. conversion into density matrices (QuantumOptics.jl) - Stefan
9. special small gates
10. make an overleaf for a paper
=#

"""
$(TYPEDEF)

Represents mixture ‚àë œï·µ¢‚±º P·µ¢ œÅ P‚±º‚Ä† where œÅ is a pure stabilizer state.

```jldoctest
julia> GeneralizedStabilizer(S"-X")
A mixture ‚àë œï·µ¢‚±º P·µ¢ œÅ P‚±º‚Ä† where œÅ is
ùíü‚ÑØùìàùìâùí∂ùí∑
+ Z
ùíÆùìâùí∂ùí∑
- X
with œï·µ¢‚±º | P·µ¢ | P‚±º:
 1.0+0.0im | + _ | + _

julia> pcT
A unitary Pauli channel P = ‚àë œï·µ¢ P·µ¢ with the following branches:
with œï·µ¢ | P·µ¢
 0.853553+0.353553im | + _
 0.146447-0.353553im | + Z

julia> apply!(GeneralizedStabilizer(S"-X"), pcT)
A mixture ‚àë œï·µ¢‚±º P·µ¢ œÅ P‚±º‚Ä† where œÅ is
ùíü‚ÑØùìàùìâùí∂ùí∑
+ Z
ùíÆùìâùí∂ùí∑
- X
with œï·µ¢‚±º | P·µ¢ | P‚±º:
 0.0+0.353553im | + _ | + Z
 0.0-0.353553im | + Z | + _
 0.853553+0.0im | + _ | + _
 0.146447+0.0im | + Z | + Z
```

See also: [`PauliChannel`](@ref)
"""
mutable struct GeneralizedStabilizer{T,F}
    stab::T
    destabweights::DefaultDict{Tuple{BitVector, BitVector}, F, F}
end

function GeneralizedStabilizer(state)
    n = nqubits(state)
    md = MixedDestabilizer(state)
    rank(md)==n || throw(ArgumentError(lazy"""
        Attempting to convert a `Stabilizer`-like object to `GeneralizedStabilizer` object failed,
        because the initial state does not represent a pure state.
        Currently only pure states can be used to initialize a `GeneralizedStabilizer` mixture of stabilizer states.
    """))
    GeneralizedStabilizer(md, DefaultDict(0.0im, (falses(n),falses(n))=>1.0+0.0im)) # TODO maybe it should default to Destabilizer, not MixedDestabilizer
end

GeneralizedStabilizer(s::GeneralizedStabilizer) = s

function Base.show(io::IO, s::GeneralizedStabilizer)
    println(io, "A mixture ‚àë œï·µ¢‚±º P·µ¢ œÅ P‚±º‚Ä† where œÅ is")
    show(io,s.stab)
    println(io)
    print(io, "with œï·µ¢‚±º | P·µ¢ | P‚±º:")
    for ((di,dj), œá) in s.destabweights
        println(io)
        print(io, " ")
        print(IOContext(io, :compact => true), œá)
        print(io, " | ", string(_stabmixdestab(s.stab, di)), " | ", string(_stabmixdestab(s.stab, dj)))
    end
end

function _stabmixdestab(mixeddestab, d)
    destab = destabilizerview(mixeddestab)
    p = zero(PauliOperator, nqubits(mixeddestab))
    for i in eachindex(d)
        if d[i]
            mul_right!(p, destab, i) # TODO check whether you are screwing up the ordering
        end
    end
    p
end

"""
Apply a Clifford gate to a generalized stabilizer state, i.e. a weighted sum of stabilizer states.

```jldoctest
julia> sm = GeneralizedStabilizer(S"-X")
A mixture ‚àë œï·µ¢‚±º P·µ¢ œÅ P‚±º‚Ä† where œÅ is
ùíü‚ÑØùìàùìâùí∂ùí∑
+ Z
ùíÆùìâùí∂ùí∑
- X
with œï·µ¢‚±º | P·µ¢ | P‚±º:
 1.0+0.0im | + _ | + _

julia> apply!(sm, CliffordOperator(tHadamard))
A mixture ‚àë œï·µ¢‚±º P·µ¢ œÅ P‚±º‚Ä† where œÅ is
ùíü‚ÑØùìàùìâùí∂ùí∑
+ X
ùíÆùìâùí∂ùí∑
- Z
with œï·µ¢‚±º | P·µ¢ | P‚±º:
 1.0+0.0im | + _ | + _
```

See also: [`GeneralizedStabilizer`](@ref)
"""
function apply!(state::GeneralizedStabilizer, gate::AbstractCliffordOperator) # TODO conjugate also the destabs
    apply!(state.stab, gate)
    state
end

"""$(TYPEDSIGNATURES)

Expectation value for the [PauliOperator](@ref) observable given the [`GeneralizedStabilizer`](@ref) state `s`.

```jldoctest
julia> sm = GeneralizedStabilizer(S"-X")
A mixture ‚àë œï·µ¢‚±º P·µ¢ œÅ P‚±º‚Ä† where œÅ is
ùíü‚ÑØùìàùìâùí∂ùí∑
+ Z
ùíÆùìâùí∂ùí∑
- X
with œï·µ¢‚±º | P·µ¢ | P‚±º:
 1.0+0.0im | + _ | + _

julia> apply!(sm, pcT)
A mixture ‚àë œï·µ¢‚±º P·µ¢ œÅ P‚±º‚Ä† where œÅ is
ùíü‚ÑØùìàùìâùí∂ùí∑
+ Z
ùíÆùìâùí∂ùí∑
- X
with œï·µ¢‚±º | P·µ¢ | P‚±º:
 0.0+0.353553im | + _ | + Z
 0.0-0.353553im | + Z | + _
 0.853553+0.0im | + _ | + _
 0.146447+0.0im | + Z | + Z

julia> expect(P"-X", sm)
0.7071067811865475 + 0.0im
```

"""
function expect(p::PauliOperator, s::GeneralizedStabilizer) # TODO optimize
    œá‚Ä≤ = zero(valtype(s.destabweights))
    phase, b, c = rowdecompose(p, s.stab)
    for ((d·µ¢,d‚±º), œá) in s.destabweights
        _allthreesumtozero(d·µ¢,d‚±º,b) || continue
        œá‚Ä≤ += œá * (-1)^(d·µ¢'*c)
    end
    return (im)^(phase) * œá‚Ä≤
end

"""Same as `all(==(0), (a.+b.+c) .% 2)`"""
function _allthreesumtozero(a,b,c) # TODO consider using bitpacking and SIMD xor with less eager shortcircuiting -- probably would be much faster
    @inbounds @simd for i in 1:length(a)
        iseven(a[i]+b[i]+c[i]) || return false
    end
    true
end

function project!(sm::GeneralizedStabilizer, p::PauliOperator)
    eval = expect(p, sm)
    prob‚ÇÅ = (real(eval)+1)/2
    error("This functionality is not implemented yet")
end

function _proj‚Çã(sm::GeneralizedStabilizer, p::PauliOperator)
end
function _proj‚Çä(sm::GeneralizedStabilizer, p::PauliOperator)
end

abstract type AbstractPauliChannel <: AbstractOperation end

"""A Pauli channel datastructure, mainly for use with [`GeneralizedStabilizer`](@ref)

See also: [`UnitaryPauliChannel`](@ref)"""
struct PauliChannel{T,S} <: AbstractPauliChannel
    paulis::T
    weights::S
    function PauliChannel(paulis, weights)
        length(paulis) == length(weights) || throw(ArgumentError(lazy"""
        Attempting to construct a `PauliChannel` failed.
        The length of the vectors of weights and of Pauli operator pairs differs
        ($(length(weights)) and $(length(paulis)) respectively).
        """))
        n = nqubits(paulis[1][1])
        for p in paulis
            n == nqubits(p[1]) == nqubits(p[2]) || throw(ArgumentError(lazy"""
            You are attempting to construct a `PauliChannel` but have provided Pauli operators
            that are not all of the same size (same number of qubits).
            Please ensure that all of the Pauli operators being provided of of the same size.
            """))
        end
        new{typeof(paulis),typeof(weights)}(paulis,weights)
    end
end

function Base.show(io::IO, pc::PauliChannel)
    println(io, "Pauli channel œÅ ‚Ü¶ ‚àë œï·µ¢‚±º P·µ¢ œÅ P‚±º‚Ä† with the following branches:")
    print(io, "with œï·µ¢‚±º | P·µ¢ | P‚±º:")
    for (i, (di,dj)) in enumerate(pc.paulis)
        œá = pc.weights[i]
        println(io)
        print(io, " ")
        print(IOContext(io, :compact => true), œá)
        print(io, " | ", di, " | ", dj)
    end
end

function embed(n::Int,idx,pc::PauliChannel)
    PauliChannel(map(p->(embed(n,idx,p[1]),embed(n,idx,p[2])),pc.paulis), pc.weights)
end

nqubits(pc::PauliChannel) = nqubits(pc.paulis[1][1])

"""Applies a (potentially non-unitary) Pauli channel to a generalized stabilizer.

See also: [`GeneralizedStabilizer`](@ref), [`PauliChannel`](@ref), [`UnitaryPauliChannel`](@ref)
"""
function apply!(state::GeneralizedStabilizer, gate::AbstractPauliChannel; prune_threshold=1e-10)
    dict = state.destabweights
    stab = state.stab
    dtype = valtype(dict)
    tzero = zero(dtype)
    tone = one(dtype)
    newdict = typeof(dict)(tzero)
    for ((d·µ¢,d‚±º), œá) in dict # the state
        for (i, (P‚Çó,P·µ£)) in enumerate(gate.paulis) # the channel
            w = gate.weights[i]
            phase‚Çó, d‚Çó, d‚ÇóÀ¢·µó·µÉ·µá = rowdecompose(P‚Çó,stab)
            phase·µ£, d·µ£, d·µ£À¢·µó·µÉ·µá = rowdecompose(P·µ£,stab)
            c = (dot(d‚ÇóÀ¢·µó·µÉ·µá,d·µ¢) + dot(d·µ£À¢·µó·µÉ·µá,d‚±º))*2
            d·µ¢‚Ä≤ = d‚Çó .‚äª d·µ¢
            d‚±º‚Ä≤ = d·µ£ .‚äª d‚±º
            œá‚Ä≤ = œá * w * (-tone)^c * (im)^(-phase‚Çó+phase·µ£+4)
            newdict[(d·µ¢‚Ä≤,d‚±º‚Ä≤)] += œá‚Ä≤
        end
    end
    filter!(x -> abs(x[2]) > prune_threshold, newdict)
    state.destabweights = newdict
    state
end

"""Decompose a Pauli ``P`` in terms of stabilizer and destabilizer rows from a given tableaux.

For given tableaux of rows destabilizer rows ``\\{d_i\\}`` and stabilizer rows ``\\{s_i\\}``,
there are boolean vectors ``b`` and ``c`` such that
``P = i^p \\prod_i d_i^{b_i} \\prod_i s_i^{c_i}``.

This function returns `p`, `b`, `c`.

```
julia> s = MixedDestabilizer(ghz(2))
ùíü‚ÑØùìàùìâùí∂ùí∑
+ Z_
+ _X
ùíÆùìâùí∂ùí∑
+ XX
+ ZZ

julia> phase, destab_rows, stab_rows = QuantumClifford.rowdecompose(P"XY", s)
(3, Bool[1, 0], Bool[1, 1])

julia> im^3 * P"Z_" * P"XX" * P"ZZ"
+ XY
```
"""
function rowdecompose(pauli,state::Union{MixedDestabilizer, Destabilizer})
    n = nqubits(pauli)
    stab = stabilizerview(state)
    dest = destabilizerview(state)
    b = falses(n)
    c = falses(n)
    P‚Çú = zero(PauliOperator, n) # TODO is this the best API choice!?
    for i in 1:n
        if comm(pauli, stab, i) != 0
            b[i] = true
            mul_right!(P‚Çú, dest, i)
        end
    end
    for i in 1:n
        if comm(pauli, dest, i) != 0
            c[i] = true
            mul_right!(P‚Çú, stab, i)
        end
    end
    p = mod(-P‚Çú.phase[],4) # complex conjugate
    return p+pauli.phase[], b, c
end

"""A Pauli channel datastructure, mainly for use with [`GeneralizedStabilizer`](@ref).

More convenient to use than [`PauliChannel`](@ref) when you know your Pauli channel is unitary.

```jldoctest
julia> Tgate = UnitaryPauliChannel(
           (I, Z),
           ((1+exp(im*œÄ/4))/2, (1-exp(im*œÄ/4))/2)
       )
A unitary Pauli channel P = ‚àë œï·µ¢ P·µ¢ with the following branches:
with œï·µ¢ | P·µ¢
 0.853553+0.353553im | + _
 0.146447-0.353553im | + Z

julia> PauliChannel(Tgate)
Pauli channel œÅ ‚Ü¶ ‚àë œï·µ¢‚±º P·µ¢ œÅ P‚±º‚Ä† with the following branches:
with œï·µ¢‚±º | P·µ¢ | P‚±º:
 0.853553+0.0im | + _ | + _
 0.0+0.353553im | + _ | + Z
 0.0-0.353553im | + Z | + _
 0.146447+0.0im | + Z | + Z
```
"""
struct UnitaryPauliChannel{T,S,P} <: AbstractPauliChannel
    paulis::T
    weights::S
    paulichannel::P # caching the conversion the more general non-unitary type of PauliChannel
    function UnitaryPauliChannel(paulis, weights)
        n = nqubits(paulis[1])
        ws = [w‚ÇÅ*w‚ÇÇ' for w‚ÇÅ in weights for w‚ÇÇ in weights]
        ps = [(p‚ÇÅ,p‚ÇÇ) for p‚ÇÅ in paulis for p‚ÇÇ in paulis]
        pc = PauliChannel(ps,ws)
        new{typeof(paulis),typeof(weights),typeof(pc)}(paulis,weights,pc)
    end
end

PauliChannel(p::UnitaryPauliChannel) = p.paulichannel

function Base.show(io::IO, pc::UnitaryPauliChannel)
    println(io, "A unitary Pauli channel P = ‚àë œï·µ¢ P·µ¢ with the following branches:")
    print(io, "with œï·µ¢ | P·µ¢")
    for (i, p) in enumerate(pc.paulis)
        œá = pc.weights[i]
        println(io)
        print(io, " ")
        print(IOContext(io, :compact => true), œá)
        print(io, " | ", p)
    end
end

function embed(n::Int,idx,pc::UnitaryPauliChannel)
    UnitaryPauliChannel(map(p->embed(n,idx,p),pc.paulis), pc.weights)
end

nqubits(pc::UnitaryPauliChannel) = nqubits(pc.paulis[1])

apply!(state::GeneralizedStabilizer, gate::UnitaryPauliChannel; prune_threshold=1e-10) = apply!(state, gate.paulichannel; prune_threshold)

##
# Predefined Pauli Channels
##

const pcT = UnitaryPauliChannel(
    (I, Z),
    ((1+exp(im*œÄ/4))/2, (1-exp(im*œÄ/4))/2)
)
